if (strcmp(mySystem,"win")==0) {
	nrn_load_dll("nmodl/nrnmech.dll")
} else if (strcmp(mySystem,"OSX")==0) {
	nrn_load_dll("/Users/gregoryschwartz/Dropbox/nNOS-2 paper/Model/nmodl/x86_64/.libs/libnrnmech.so")
}

load_file("cellFromMatlabNetwork.hoc")
objectvar nNOS[nCells]
strdef fName
objref XY
XY = new Matrix()

objref nDend
nDend = new Vector() //# of dendrites in each cell
totalDend = 0

for i = 0,nCells-1 {
	sprint(fName,"%s/%.4d.dat",cellDir,i+1)
	XY = loadFromMatlab(fName)
	nDend.append(XY.nrow)
	totalDend = totalDend + XY.nrow
	nNOS[i] = new cellFromMatlab(XY)
}
midCell = (nCells+sqrt(nCells))/2-1

objref stimFile

objref cellInd, dendInd, dendFrac

cellInd = new Vector()
dendInd = new Vector()
dendFrac = new Vector()

//read stims
stimFile = new File()
stimFile.ropen(stimFileName)
while (!stimFile.eof()) {
		cellInd.append(stimFile.scanvar())
		dendInd.append(stimFile.scanvar())
		dendFrac.append(stimFile.scanvar())
}
stimFile.close()

// stims
objref IC[cellInd.size()]
for i=0,cellInd.size()-1 {
	if (dendInd.x[i] < 2) {
		//initial branches 
		error()
		nNOS[cellInd.x[i]].dend[dendInd.x[i]] {
			IC[i] = new IClamp(dendFrac.x[i])
			IC[i].del = 100
			IC[i].dur = 100
			IC[i].amp = 0.005 / 86.66 //to correct for 86x greater density of sampling on these dendrites	
		}
	} else {
		nNOS[cellInd.x[i]].dend[dendInd.x[i]] {
			IC[i] = new IClamp(dendFrac.x[i])
			IC[i].del = 500
			IC[i].dur = 100
			IC[i].amp = 0.010
		}
	}
}

tstop=1499.9
v_init=-64.0
dt=1
celsius=31.0

// diameter set from function

objref SL,RAN
SL = new SectionList()
RAN = new Random()
RAN.normal(1,0.3^2)
for i=0,nCells-1 {
	nNOS[i].soma {
		distance()
		SL.remove(SL)
		SL.wholetree()
	}
	forsec SL {
		if (distance(0)!=0) { //not soma
			/*for j=0,99 {
				diam(j/100:(j+1)/100) = 1.098*exp(-0.03926*distance(j/100))+1.395*exp(-0.0003946*distance(j/100)):1.098*exp(-0.03926*distance((j+1)/100))+1.395*exp(-0.0003946*distance((j+1)/100))

			}*/
			if (diam!=2) { //not neck
				nS = nseg
				for j=0,nS-1 {
					q = RAN.repick()
					if (q<0.5) {
						q = 0.5
					}
					diam(j/nS:(j+1)/nS) = q:q
				}
			}
		}
	}
}

//gap junction connectivity
sprint(fName,"%s/junc.dat",cellDir) //NB: all connections are reciprocal, but NEURON only does unidirectional connections
XY = parseMatlabConnectivity(fName)
nGaps = XY.nrow()
objref junc[nGaps]
for j = 0, nGaps-1 {
	junc[j] = new gap()
	// column 0: cell making connection
	// column 1: cell receiving connection
	// column 2: branch making connection
	// column 3: branch receiving connection
	// column 4: proportional distance along branch making connection
	// column 5: proportional distance along branch receiving connection
	nNOS[XY.x[j][0]].dend[XY.x[j][2]] junc[j].loc(XY.x[j][4]) 
	// There is a bug in NEURON with range variables accepting array input
	temp = XY.x[j][5]
	setpointer junc[j].vgap, nNOS[XY.x[j][1]].dend[XY.x[j][3]].v(temp)
	junc[j].r = 9.13e4 
}


// biophys
forall insert pas
forall e_pas = v_init
//forall g_pas = 1/(1.289e5)
forall g_pas = 1/8e4

// procs
load_file("params.hoc")
load_file("rn.hoc")
load_file("lambda.hoc")
load_file("gapOnOff.hoc")

for i=0,nCells-1 {
	nNOS[i].soma insert extr
}

Rm = 8e4






